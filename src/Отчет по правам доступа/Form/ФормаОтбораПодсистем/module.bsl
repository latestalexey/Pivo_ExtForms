
Перем УчитыватьИерархиюПодсистем;

Процедура ПриОткрытии()
	
	ТабличноеПолеДереваПодсистем = ЭлементыФормы.ДеревоПодсистемКонфигурации;
	ТабличноеПолеДереваПодсистем.СоздатьКолонки();
	
	КолонкиДерева = ТабличноеПолеДереваПодсистем.Колонки;
		
	КолонкиДерева.МетаданныеПодсистемы.Видимость   = Ложь;
	КолонкиДерева.СинонимПодсистемы.ТолькоПросмотр = Истина;
	
	КолонкаПометки = КолонкиДерева.Пометка;
	//КолонкаПометки.ТриСостоянияФлажка = Истина;
	КолонкаПометки.Ширина = 8;
	КолонкаПометки.Данные = "";
	КолонкаПометки.ДанныеФлажка = "Пометка";
	КолонкаПометки.РежимРедактирования = РежимРедактированияКолонки.Непосредственно;
	
КонецПроцедуры

Процедура ДеревоПодсистемКонфигурацииПриИзмененииФлажка(Элемент, Колонка)
	
	Если НЕ УчитыватьИерархиюПодсистем Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДереваПодсистем = Элемент.ТекущаяСтрока;
	
	УстановитьПометкуПодчиненныхСтрок(СтрокаДереваПодсистем);	
	
КонецПроцедуры

Процедура УстановитьПометкуРодительскихСтрок(СтрокаДереваПодсистем)
	
	РодительСтрокиДерева = СтрокаДереваПодсистем.Родитель;
	
	Если РодительСтрокиДерева = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокУбранаПометка = 0;
	СтрокиРодителя     = РодительСтрокиДерева.Строки;
	Для каждого СтрокаРодителя Из СтрокиРодителя Цикл		
		СтрокУбранаПометка = СтрокУбранаПометка + ?(СтрокаРодителя.Пометка, 0, 1);
	КонецЦикла;
	
	РодительСтрокиДерева.Пометка = СтрокУбранаПометка <> СтрокиРодителя.Количество();
	УстановитьПометкуРодительскихСтрок(РодительСтрокиДерева);
	
КонецПроцедуры

Процедура УстановитьПометкуПодчиненныхСтрок(СтрокаДерева)
	
	Для каждого ПодчиненнаяСтрокаДерева Из СтрокаДерева.Строки Цикл
		ПодчиненнаяСтрокаДерева.Пометка = СтрокаДерева.Пометка;
		УстановитьПометкуПодчиненныхСтрок(ПодчиненнаяСтрокаДерева);
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельДереваУстановитьФлажки(Кнопка)
		
	Для каждого СтрокаДерева Из ДеревоПодсистемКонфигурации.Строки Цикл
		СтрокаДерева.Пометка = Истина;
		УстановитьПометкуПодчиненныхСтрок(СтрокаДерева);
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельДереваСнятьФлажки(Кнопка)
		
	Для каждого СтрокаДерева Из ДеревоПодсистемКонфигурации.Строки Цикл
		СтрокаДерева.Пометка = Ложь;
		УстановитьПометкуПодчиненныхСтрок(СтрокаДерева);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДеревоПодсистемКонфигурацииПриПолученииДанных(Элемент, ОформленияСтрок)
	
	Для каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		ОформлениеСтроки.Шрифт = ?(ОформлениеСтроки.ДанныеСтроки.Пометка, ШрифтыСтиля.ШрифтВажнойНадписи, ШрифтыСтиля.ШрифтТекста);
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельДереваСвернутьДерево(Кнопка)
	
	Для каждого СтрокаДереваПодсистем Из ДеревоПодсистемКонфигурации.Строки Цикл
		ЭлементыФормы.ДеревоПодсистемКонфигурации.Свернуть(СтрокаДереваПодсистем);
	КонецЦикла;
	
КонецПроцедуры

Процедура КоманднаяПанельДереваРазвернутьДерево(Кнопка)
	
	Для каждого СтрокаДереваПодсистем Из ДеревоПодсистемКонфигурации.Строки Цикл
		ЭлементыФормы.ДеревоПодсистемКонфигурации.Развернуть(СтрокаДереваПодсистем, Истина);
	КонецЦикла
	
КонецПроцедуры

Процедура КоманднаяПанельДереваУчитыватьИерархию(Кнопка)
	
	Кнопка.Пометка = НЕ Кнопка.Пометка;
	УчитыватьИерархиюПодсистем = Кнопка.Пометка;
	
	Если УчитыватьИерархиюПодсистем Тогда
		Для каждого СтрокаДереваПодсистем Из ДеревоПодсистемКонфигурации.Строки  Цикл
			УстановитьПометкуПодчиненныхСтрок(СтрокаДереваПодсистем);	
			УстановитьПометкуРодительскихСтрок(СтрокаДереваПодсистем);		
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

УчитыватьИерархиюПодсистем = Ложь;