 Перем СЗДругиеПоддерживаемыеКонфигурации Экспорт;
 Перем мНазваниеКонфигурации Экспорт; 
 Перем мВерсияКонфигурации Экспорт;
 Перем ПутьК_XML Экспорт; // ZHKN. 09.07.2014. №152 Мобильная печать 
 Перем мНастройкиМобильнойПечати Экспорт; // ZHKN. 09.07.2014. №152 Мобильная печать 
 Перем мПараметрыМобильнойПечати Экспорт; // ZHKN. 09.07.2014. №152 Мобильная печать

Функция ПоддержкаМодификацийРозницы(мНазваниеКонфигурации) Экспорт

	Возврат  СЗДругиеПоддерживаемыеКонфигурации.НайтиПоЗначению(мНазваниеКонфигурации) <> неопределено;
	
КонецФункции

Функция ПолучитьВерсиюКонфигурации() Экспорт
	
	Попытка
		ВерсияВМассив =  РазложитьСтрокуВМассивПодстрок_(метаданные.Версия, ".");
		a0 = ВерсияВМассив.Получить(0);
		a1 = ВерсияВМассив.Получить(1);
		a2 = ВерсияВМассив.Получить(2);
		a3 = ВерсияВМассив.Получить(3);
		Версия =  ?(СтрДлина(a0)=1,"0"+a0,a0)+?(СтрДлина(a1)=1,"0"+a1,a1)+?(СтрДлина(a2)=1,"0"+a2,a2)+?(СтрДлина(a3)=1,"0"+a3,a3);		
		Возврат Число(СтрЗаменить(Версия,".",""));
	Исключение
		Результат =  "Неправильный формат версии конфигурации базы 1С: """+метаданные.версия+"""! 
		|В версии должны присутствовать только цифры и символ ""."". 
		|В противном случае возможны ошибки в работе драйвера, т.к. в разных версиях конфигураций могут отличаться названия процедур и функций, названия переменных, 
		|процедуры могут быть перенесены в другой общий модуль и т.д. ";
		Предупреждение(Результат,20,"Изменение данных");
 		Сообщить(Результат,СтатусСообщения.Важное);
 		Возврат 1;
	КонецПопытки;
	
КонецФункции

Функция РазложитьСтрокуВМассивПодстрок_(Знач Стр, Разделитель = ",") Экспорт
	
	МассивСтрок = Новый Массив();
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока 1 = 1 Цикл
			Поз = Найти(Стр, Разделитель);
			Если Поз = 0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр, Поз - 1));
			Стр = СокрЛ(Сред(Стр, Поз));
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока 1 = 1 Цикл
			Поз = Найти(Стр, Разделитель);
			Если Поз = 0 Тогда
				Если (СокрЛП(Стр) <> "") Тогда
					МассивСтрок.Добавить(Стр);
				КонецЕсли;
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз - 1));
			Стр = Сред(Стр, Поз + ДлинаРазделителя);
		КонецЦикла;
	КонецЕсли;
	
КонецФункции 

// +ZHKN. 09.07.2014. №152 Мобильная печать
Функция ПолучитьПутьКНстройкамМобильнойПечати() Экспорт
	
	сПуть = "";
	
	хШелл = Новый COMОбъект("Wscript.Shell");
	Путь_AppData = хШелл.ExpandEnvironmentStrings("%ALLUSERSPROFILE%");
	
	сПуть = Путь_AppData + "\Cleverence\Driver1C";
	мФайл = Новый Файл(сПуть);
	
	Если Не мФайл.Существует() Тогда
		
		сПуть = Путь_AppData + "\Application Data\Cleverence\Driver1C";	
		мФайл = Новый Файл(сПуть);
		Если Не мФайл.Существует() Тогда
			сПуть = Путь_AppData;	
		КонецЕсли;	
	КонецЕсли;

	сПуть = сПуть + "\CleverenceStickerForRevaluationSettings.xml";
	Возврат сПуть;	
КонецФункции 

Функция СохранитьНастройкиМобильнойПечатиПоУмолчанию() Экспорт
	
	ЗаписьXML = Новый ЗаписьXML;
	Попытка
		ЗаписьXML.ОткрытьФайл(ПутьК_XML);
	Исключение
		Сообщить("Не удалось создать файл настроек XML");
		Возврат Ложь;
	КонецПопытки;

    ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Настройки");
		ЗаписьXML.ЗаписатьНачалоЭлемента("ВидЭтикетки"); 
		ЗаписьXML.ЗаписатьТекст("Товарная");
        ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("ПоляЭтикетки");
        ЗаписьXML.ЗаписатьКонецЭлемента();
		
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	
	Возврат Истина;	
КонецФункции

Функция ЗагрузитьНастройкиМобильнойПечати() Экспорт
	
	мНастройкиМобильнойПечати.Очистить();
	
	ФайлXML = Новый ЧтениеXML;
			
	Попытка
		ФайлXML.ОткрытьФайл(ПутьК_XML);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Пока ФайлXML.Прочитать() Цикл
		
		Если ФайлXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Если ФайлXML.Имя = "Настройки" Тогда
				Продолжить;	
			КонецЕсли;
			Если ФайлXML.Имя <> "ВидЭтикетки" Тогда
				ФайлXML.Закрыть();
				Возврат Ложь;
			КонецЕсли;	
		КонецЕсли;
		
		Если ФайлXML.ТипУзла = ТипУзлаXML.Текст Тогда
			мНастройкиМобильнойПечати.Добавить("ВидЭтикетки", ФайлXML.Значение); 	
		КонецЕсли;
		
		Если ФайлXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Прервать;	
		КонецЕсли;
		
	КонецЦикла;
	
	Пока ФайлXML.Прочитать() Цикл
		Если ФайлXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда	
			Продолжить;	
		КонецЕсли;	
		
		Если ФайлXML.Имя = "ПоляЭтикетки" Тогда
			Продолжить;
		КонецЕсли;
		
		мНастройкиМобильнойПечати.Добавить(ФайлXML.Имя); 
		
	КонецЦикла;	
	
	ФайлXML.Закрыть();
	
	Возврат Истина;	
	
КонецФункции

Функция СохранитьНастройкиМобильнойПечати() Экспорт
	
	ЗаписьXML = Новый ЗаписьXML;
	Попытка
		ЗаписьXML.ОткрытьФайл(ПутьК_XML);
	Исключение
		Сообщить("Не удалось открыть файл настроек XML");
		Возврат Ложь;
	КонецПопытки;

    ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Настройки");
		ЗаписьXML.ЗаписатьНачалоЭлемента("ВидЭтикетки"); 
		мЭлемент = мНастройкиМобильнойПечати.НайтиПоЗначению("ВидЭтикетки");
		Если мЭлемент = Неопределено Тогда 
			ЗаписьXML.Закрыть();
			Возврат Ложь;
		КонецЕсли;
		ЗаписьXML.ЗаписатьТекст(мЭлемент.Представление);
        ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("ПоляЭтикетки");
			Для каждого мЭлемент Из мНастройкиМобильнойПечати Цикл	
				Если мЭлемент.Значение = "ВидЭтикетки" Тогда
					Продолжить;	
				КонецЕсли;
				
				ЗаписьXML.ЗаписатьНачалоЭлемента(мЭлемент.Значение);
				ЗаписьXML.ЗаписатьКонецЭлемента();
			КонецЦикла;
        ЗаписьXML.ЗаписатьКонецЭлемента();
		
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	ЗаписьXML.Закрыть();
	
	
	Возврат Истина;	
КонецФункции
// -ZHKN. 09.07.2014.
  
СЗДругиеПоддерживаемыеКонфигурации = Новый СписокЗначений;
СЗДругиеПоддерживаемыеКонфигурации.Добавить("салонкрасоты");
СЗДругиеПоддерживаемыеКонфигурации.Добавить("книжныймагазин");

мНазваниеСохраненнойНастройкиЗаполненияДокументов  = "КлеверенсНЗД"; // в некоторых конфигурациях ограничена длина названия.
мНазваниеКонфигурации   = Нрег(Метаданные.Имя);
мВерсияКонфигурации = ПолучитьВерсиюКонфигурации();
// +ZHKN. 09.07.2014. №152 Мобильная печать
мПараметрыМобильнойПечати = Новый СписокЗначений;
мПараметрыМобильнойПечати.Добавить("AdditionInfo", "Доп. информация"); 			
мПараметрыМобильнойПечати.Добавить("data", "Дата"); 							
мПараметрыМобильнойПечати.Добавить("ExpiredDate", "Срок годности");             
мПараметрыМобильнойПечати.Добавить("oldPrice", "Старая цена");                  
мПараметрыМобильнойПечати.Добавить("sellerName", "Имя продавца"); 				
мПараметрыМобильнойПечати.Добавить("sellerAddress", "Адрес продавца"); 			
мПараметрыМобильнойПечати.Добавить("producerCountry", "Страна производства");   
мПараметрыМобильнойПечати.Добавить("producerName", "Имя производителя");		
// мПараметрыМобильнойПечати.Добавить("producerAddress", "Адрес производителя");
// мПараметрыМобильнойПечати.Добавить("RegistrationDate", "Дата регистрации");
мПараметрыМобильнойПечати.Добавить("supplierName", "Имя поставщика"); 			
мПараметрыМобильнойПечати.Добавить("supplierAddress", "Адрес поставщика"); 		
//мПараметрыМобильнойПечати.Добавить("marking", "Артикул товара");

ПутьК_XML = ПолучитьПутьКНстройкамМобильнойПечати();
мНастройкиМобильнойПечати = Новый СписокЗначений;
Если Не ЗагрузитьНастройкиМобильнойПечати() Тогда
	СохранитьНастройкиМобильнойПечатиПоУмолчанию();	
	Если Не ЗагрузитьНастройкиМобильнойПечати() Тогда
		Сообщить("Не удалось загрузить настройки этикеток для переоценки", СтатусСообщения.Важное);
	КонецЕсли;
КонецЕсли;
// -ZHKN. 09.07.2014.

