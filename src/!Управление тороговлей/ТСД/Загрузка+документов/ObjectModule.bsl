Перем СЗДругиеПоддерживаемыеКонфигурации Экспорт;
Перем мНазваниеКонфигурации Экспорт; 
Перем мИмяДляХраненияНастроек Экспорт;
Перем мНазваниеСохраненнойНастройкиЗаполненияДокументов Экспорт;
Перем СтруктураОбъектаХраненияШтрихкодов Экспорт;
Перем мВерсияКонфигурации Экспорт;
 
Функция ПолучитьСписокДокументовПоУмолчнию() Экспорт
	
	Спис = Новый СписокЗначений;
	
	Спис.Добавить("ЗаказПокупателя");
	Спис.Добавить("ЗаказПоставщику");
	Спис.Добавить("ИнвентаризацияТоваровНаСкладе");
	Спис.Добавить("ИнвентаризацияТоваров");
	Спис.Добавить("ПеремещениеТоваров");
    Спис.Добавить("ПереоценкаТоваровВРознице");
	Спис.Добавить("ПоступлениеТоваровУслуг");
	Спис.Добавить("РазмещениеЗаказа");
	Спис.Добавить("РеализацияТоваровУслуг");
	Спис.Добавить("РезервированиеТоваров");
	Спис.Добавить("СписаниеТоваров");
	Спис.Добавить("УстановкаЦенНоменклатуры");
	Возврат Спис;
	
КонецФункции

Функция ПоддержкаДругихВерсий() Экспорт

	Если Найти(мНазваниеКонфигурации,"розница")>0 и ПолучитьВерсиюКонфигурации() < 2000000 Тогда //ZHKN. 23.01.2015. №303  Ошибки в Рознице 1.0
	  Возврат Истина;	
	КонецЕсли;
	
	Возврат  СЗДругиеПоддерживаемыеКонфигурации.НайтиПоЗначению(мНазваниеКонфигурации) <> неопределено;
	
КонецФункции

Функция ПолучитьВерсиюКонфигурации() Экспорт
	
	Попытка
		ВерсияВМассив =  РазложитьСтрокуВМассивПодстрок_(метаданные.Версия, ".");
		a0 = ВерсияВМассив.Получить(0);
		a1 = ВерсияВМассив.Получить(1);
		a2 = ВерсияВМассив.Получить(2);
		a3 = ВерсияВМассив.Получить(3);
		Версия =  ?(СтрДлина(a0)=1,"0"+a0,a0)+?(СтрДлина(a1)=1,"0"+a1,a1)+?(СтрДлина(a2)=1,"0"+a2,a2)+?(СтрДлина(a3)=1,"0"+a3,a3);		
		Возврат Число(СтрЗаменить(Версия,".",""));
	Исключение
		Результат =  "Неправильный формат версии конфигурации базы 1С: """+метаданные.версия+"""! 
		|В версии должны присутствовать только цифры и символ ""."". 
		|В противном случае возможны ошибки в работе драйвера, т.к. в разных версиях конфигураций могут отличаться названия процедур и функций, названия переменных, 
		|процедуры могут быть перенесены в другой общий модуль и т.д. ";
		Предупреждение(Результат,20,"Изменение данных");
 		Сообщить(Результат,СтатусСообщения.Важное);
 		Возврат 1;
	КонецПопытки;
	
КонецФункции

Функция РазложитьСтрокуВМассивПодстрок_(Знач Стр, Разделитель = ",") Экспорт
	
	МассивСтрок = Новый Массив();
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока 1 = 1 Цикл
			Поз = Найти(Стр, Разделитель);
			Если Поз = 0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр, Поз - 1));
			Стр = СокрЛ(Сред(Стр, Поз));
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока 1 = 1 Цикл
			Поз = Найти(Стр, Разделитель);
			Если Поз = 0 Тогда
				Если (СокрЛП(Стр) <> "") Тогда
					МассивСтрок.Добавить(Стр);
				КонецЕсли;
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз - 1));
			Стр = Сред(Стр, Поз + ДлинаРазделителя);
		КонецЦикла;
	КонецЕсли;
	
КонецФункции 

Функция ПолучитьСтруктуруОбъектаХраненияШтрихкодов() Экспорт //скопировано из конфигурации 1С драйвера ПРОФ, но с изменениями некоторымы - без учета УФ
	
	ДополнительныйОтборТекст = "";
	
	Если Найти(мНазваниеКонфигурации,"розница")>0 Тогда
 			ОбъектМетаданных                     = "РегистрСведений.ШтрихКоды";
			НазваниеРеквизитаНоменклатура        = "Владелец";
			НазваниеРеквизитаУпаковка            = "ЕдиницаИзмерения";
			НазваниеРеквизитаХарактеристика      = "ХарактеристикаНоменклатуры";
			НазваниеРеквизитаГдеХранитсяШтрихКод = "ШтрихКод"; 
 	ИначеЕсли Найти(мНазваниеКонфигурации,"далионуправлениемагазином") > 0 тогда
		ОбъектМетаданных                     = "РегистрСведений.ШтрихКоды";
		НазваниеРеквизитаНоменклатура        = "Номенклатура";
		НазваниеРеквизитаУпаковка            = "ЕдиницаИзмерения";
		НазваниеРеквизитаХарактеристика      = "ХарактеристикаНоменклатуры";
		НазваниеРеквизитаГдеХранитсяШтрихКод = "ШтрихКод"; 
        ДополнительныйОтборТекст             = " И не РегШК.НеИспользуется ";
	ИначеЕсли Найти(мНазваниеКонфигурации, "управлениенебольшойфирмой") > 0 Тогда	
		ОбъектМетаданных                     = "РегистрСведений.ШтрихкодыНоменклатуры";
		НазваниеРеквизитаНоменклатура        = "Номенклатура";
		НазваниеРеквизитаУпаковка            = "ЕдиницаИзмерения";
		НазваниеРеквизитаХарактеристика      = "Характеристика";
		НазваниеРеквизитаГдеХранитсяШтрихКод = "ШтрихКод"; 
	ИначеЕсли  Найти(мНазваниеКонфигурации, "штрихм") > 0 	Тогда
 		ОбъектМетаданных                     = "Справочник.ШтрихКоды";
		НазваниеРеквизитаНоменклатура        = "Владелец";
		НазваниеРеквизитаУпаковка            = "Единица";
		НазваниеРеквизитаХарактеристика      = "ХарактеристикаНоменклатуры";
		НазваниеРеквизитаГдеХранитсяШтрихКод = "Наименование"; 
	ИначеЕсли Найти(мНазваниеКонфигурации, "автосалон5") > 0  Тогда
		ОбъектМетаданных                     = "РегистрСведений.ШтрихКоды";
		НазваниеРеквизитаНоменклатура        = "Объект";
		НазваниеРеквизитаУпаковка            = "ЕдиницаИзмерения";
		НазваниеРеквизитаХарактеристика      = "ХарактеристикаНоменклатуры";
		НазваниеРеквизитаГдеХранитсяШтрихКод = "ШтрихКод";  	
	иначе
		ОбъектМетаданных                     = "РегистрСведений.Штрихкоды";
		НазваниеРеквизитаНоменклатура        = "Владелец";
		НазваниеРеквизитаУпаковка            = "ЕдиницаИзмерения";
		НазваниеРеквизитаХарактеристика      = "ХарактеристикаНоменклатуры";
		НазваниеРеквизитаГдеХранитсяШтрихКод = "ШтрихКод"; 	
	КонецЕсли;
	
	СтруктуруОбъекта = Новый Структура();
	СтруктуруОбъекта.Вставить("ОбъектМетаданных",ОбъектМетаданных);
	СтруктуруОбъекта.Вставить("НазваниеРеквизитаНоменклатура",НазваниеРеквизитаНоменклатура);
	СтруктуруОбъекта.Вставить("НазваниеРеквизитаУпаковка",НазваниеРеквизитаУпаковка);
	СтруктуруОбъекта.Вставить("НазваниеРеквизитаХарактеристика",НазваниеРеквизитаХарактеристика);
	СтруктуруОбъекта.Вставить("НазваниеРеквизитаГдеХранитсяШтрихКод",НазваниеРеквизитаГдеХранитсяШтрихКод);
	СтруктуруОбъекта.Вставить("ДополнительныйОтборТекст",ДополнительныйОтборТекст);
	
	Возврат СтруктуруОбъекта;

КонецФункции

Функция ШтрихКодЕстьВБазе(НаписатьСообщение,Штрихкод) Экспорт
	
	ШтрихКодЕстьВБазе = Ложь;
	Запрос = новый Запрос;
	Запрос.Текст = 		
	"ВЫБРАТЬ
	|    РегШК."+СтруктураОбъектаХраненияШтрихкодов.НазваниеРеквизитаГдеХранитсяШтрихКод+" КАК Штрихкод,
	|    РегШК."+СтруктураОбъектаХраненияШтрихкодов.НазваниеРеквизитаНоменклатура+" КАК Владелец
	|ИЗ
	|    "+СтруктураОбъектаХраненияШтрихкодов.ОбъектМетаданных+"  КАК РегШК
	|ГДЕ
	|	РегШК."+СтруктураОбъектаХраненияШтрихкодов.НазваниеРеквизитаГдеХранитсяШтрихКод+" = &Штрихкод";   
	Запрос.УстановитьПараметр("ШтрихКод", Штрихкод);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ШтрихКодЕстьВБазе = Истина;
		Если НаписатьСообщение тогда
			Сообщить("Штрихкод: " + Штрихкод + " уже имеет владельца """ + СокрЛП(Выборка.Владелец) + """.");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ШтрихКодЕстьВБазе;
	
КонецФункции

Процедура ВыполнитьДействия(ТекДок, ИмяТаблицы, ТабличноеПоле) Экспорт

	Если ТекДок[ИмяТаблицы].Количество() > 0 Тогда
		Если Вопрос("Очистить табличную часть документа ?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
			ТекДок[ИмяТаблицы].Очистить();
		КонецЕсли;
	КонецЕсли;
	
	ФормаОбработки = ЭтотОбъект.ПолучитьФорму("Форма");
	ФормаОбработки.Открыть();
	
	ТЗТовары = Новый ТаблицаЗначений;
	
	ТЗТовары = ЭтотОбъект.Товары.Выгрузить();
	Если ТЗТовары.Количество() > 0 Тогда
		ТЗТовары.Свернуть("Код, Штрихкод, Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения, Цена","КоличествоФакт, КоличествоПлан");
	КонецЕсли;
	
	Для каждого Стр из ТЗТовары Цикл
	    
		Если ТекДок.Метаданные().Имя = "ПриходнаяНакладная" Тогда
			
			Нашли = 0;
			Для каждого СтрДок Из ТекДок[ИмяТаблицы] Цикл
			
				Если (СтрДок.Номенклатура = Стр.Номенклатура) И (СтрДок.Единица = Стр.ЕдиницаИзмерения)
				   И (СтрДок.ХарактеристикаНоменклатуры = Стр.ХарактеристикаНоменклатуры) Тогда
				   				   			
					Нашли = 1;
					СтрДок.Количество     = СтрДок.Количество + Стр.КоличествоФакт;
					СтрДок.ПриходнаяСумма = СтрДок.ПриходнаяЦена * СтрДок.Количество;
					СтрДок.Всего          = СтрДок.ПриходнаяЦена * СтрДок.Количество;
					СтрДок.РозничнаяСумма = СтрДок.ПриходнаяЦена * СтрДок.Количество;
					
				КонецЕсли;
			
			КонецЦикла;
			
			Если Нашли = 0 Тогда
			
				НовТовар = ТекДок[ИмяТаблицы].Добавить();
				НовТовар.Номенклатура   = Стр.Номенклатура;
				НовТовар.ХарактеристикаНоменклатуры = Стр.ХарактеристикаНоменклатуры;
				НовТовар.Единица        = Стр.ЕдиницаИзмерения;
				НовТовар.Коэффициент    = Стр.ЕдиницаИзмерения.Коэффициент;
				НовТовар.ПриходнаяЦена  = Стр.Цена;
				НовТовар.ПриходнаяСумма = Стр.Цена * Стр.КоличествоФакт;
				НовТовар.Всего          = Стр.Цена * Стр.КоличествоФакт;
				НовТовар.РозничнаяЦена  = Стр.Цена;
				НовТовар.РозничнаяСумма = Стр.Цена * Стр.КоличествоФакт;
				НовТовар.Количество     = Стр.КоличествоФакт;
				
			КонецЕсли;
			
			ТекДок.Записать(РежимЗаписиДокумента.Запись);
			
		ИначеЕсли ТекДок.Метаданные().Имя = "ВыгрузкаИзТСД" Тогда
			
			Нашли = 0;
			Для каждого СтрДок Из ТекДок[ИмяТаблицы] Цикл
			
				Если (СтрДок.Номенклатура = Стр.Номенклатура) И (СтрДок.Единица = Стр.ЕдиницаИзмерения)
				   И (СтрДок.ХарактеристикаНоменклатуры = Стр.ХарактеристикаНоменклатуры) Тогда
				   				   			
					Нашли = 1;
					СтрДок.Количество = СтрДок.Количество + Стр.КоличествоФакт;
				
				КонецЕсли;
			
			КонецЦикла;
			
			Если Нашли = 0 Тогда
				
				НовТовар = ТекДок[ИмяТаблицы].Добавить();
				НовТовар.Номенклатура   = Стр.Номенклатура;
				НовТовар.ХарактеристикаНоменклатуры = Стр.ХарактеристикаНоменклатуры;
				НовТовар.Единица        = Стр.ЕдиницаИзмерения;
				НовТовар.Коэффициент    = Стр.ЕдиницаИзмерения.Коэффициент;
				НовТовар.Количество     = Стр.КоличествоФакт;
			
			КонецЕсли;
			
			ТекДок.Записать(РежимЗаписиДокумента.Запись);
			
		Иначе
			
			Попытка
				Нашли = 0;
				Для каждого СтрДок Из ТекДок[ИмяТаблицы] Цикл
				
					Если (СтрДок.Номенклатура = Стр.Номенклатура) И (СтрДок.Единица = Стр.ЕдиницаИзмерения)
					   И (СтрДок.ХарактеристикаНоменклатуры = Стр.ХарактеристикаНоменклатуры) Тогда
					   				   			
						Нашли = 1;
						СтрДок.Количество = СтрДок.Количество + Стр.КоличествоФакт;
					
					КонецЕсли;
				
				КонецЦикла;
				
				Если Нашли = 0 Тогда
					
					НовТовар = ТекДок[ИмяТаблицы].Добавить();
					НовТовар.Номенклатура   = Стр.Номенклатура;
					НовТовар.ХарактеристикаНоменклатуры = Стр.ХарактеристикаНоменклатуры;
					НовТовар.Единица        = Стр.ЕдиницаИзмерения;
					НовТовар.Коэффициент    = Стр.ЕдиницаИзмерения.Коэффициент;
					НовТовар.Количество     = Стр.КоличествоФакт;
				
				КонецЕсли;
			Исключение
			    Сообщить("Имена полей таблицы данного документа не оостветствуют заданным.");
			КонецПопытки;
		КонецЕсли;
	
	КонецЦикла;
	
	ФормаОбработки.Закрыть();

КонецПроцедуры

СЗДругиеПоддерживаемыеКонфигурации = Новый СписокЗначений;
СЗДругиеПоддерживаемыеКонфигурации.Добавить("салонкрасоты");
СЗДругиеПоддерживаемыеКонфигурации.Добавить("книжныймагазин");

мНазваниеСохраненнойНастройкиЗаполненияДокументов  = "КлеверенсНЗД"; // в некоторых конфигурациях ограничена длина названия.
мНазваниеКонфигурации   = Нрег(Метаданные.Имя);
СтруктураОбъектаХраненияШтрихкодов =  ПолучитьСтруктуруОбъектаХраненияШтрихкодов();
мВерсияКонфигурации = ПолучитьВерсиюКонфигурации();
