Перем НажатаКнопкаOK Экспорт;
Перем ТекущийТипДокумента Экспорт;

Процедура ПриОткрытии()	
	
	ТаблицаДокументов.Колонки.Добавить("Выгружать");
	
	колонка = ЭтаФорма.ЭлементыФормы.ТаблицаДокументов.Колонки.Вставить(0, "Выгружать");
	колонка.УстановитьЭлементУправления(Тип("Флажок"));
	колонка.Имя = "Выгружать";
	колонка.Данные = "Выгружать";
	колонка.ДанныеФлажка = "Выгружать";
	колонка.Формат = "БЛ= ; БИ= ";
	колонка.РежимРедактирования = РежимРедактированияКолонки.Непосредственно;
	колонка.Ширина = 12;
	колонка.ГоризонтальноеПоложениеВКолонке = ГоризонтальноеПоложение.Центр;
	
	Спис = ВосстановитьЗначение(ЭтаФорма.ВладелецФормы.мИмяДляХраненияНастроек+"_ФормаСпискаДокументов"); 
	
	ТекущаяСтрока = Неопределено;
	
	Для каждого ОбъектДок из Метаданные.Документы Цикл
		
		ДобавитьДокументВСписокВыбора = Ложь; 
		
		Для каждого ТабличнаяЧастьДокумента из ОбъектДок.ТабличныеЧасти Цикл
			Если ТабличнаяЧастьДокумента.Реквизиты.Найти("Номенклатура")<> Неопределено Тогда
				ДобавитьДокументВСписокВыбора = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ДобавитьДокументВСписокВыбора Тогда
			Продолжить;	
		КонецЕсли;
		
		мНоваястрока = ТаблицаДокументов.Добавить();
		
		мНоваястрока.Выгружать = Ложь;
		мНоваястрока.Документ = ОбъектДок.Синоним; 
		мНоваястрока.ТипДокумента = ОбъектДок.Имя; 
		мНоваястрока.НастройкаЗаполнения = "";
		
		Если мНоваястрока.ТипДокумента = ТекущийТипДокумента Тогда
			ТекущаяСтрока = мНоваястрока;
		КонецЕсли;
		
		Если Спис <> Неопределено И Спис.НайтиПоЗначению(ОбъектДок.Имя) <> Неопределено Тогда
			мНоваястрока.Выгружать = Истина;	
		КонецЕсли;
		
		мСохрФормат = ВосстановитьЗначение(ЭтаФорма.ВладелецФормы.мИмяДляХраненияНастроек);
		Если мСохрФормат <> Неопределено И мСохрФормат[ОбъектДок.Имя] <> Неопределено Тогда
			мНоваястрока.НастройкаЗаполнения = мСохрФормат[ОбъектДок.Имя].ТипДокументаСмартс;	
		КонецЕсли;
		
	КонецЦикла;	
	
	// + ZHKN. 07.03.2014. №108.  Сортировка типов документов.
	// ТаблицаДокументов.Сортировать("Выгружать УБЫВ, НастройкаЗаполнения УБЫВ, Документ Возр");
	ТаблицаДокументов.Сортировать("Выгружать УБЫВ,Документ Возр");
	// - ZHKN. 07.03.2014.
	
	Если ТекущаяСтрока <> Неопределено Тогда 
		ЭлементыФормы.ТаблицаДокументов.ТекущаяСтрока = ТекущаяСтрока; 	
		ЭлементыФормы.ТаблицаДокументов.ТекущаяКолонка = ЭлементыФормы.ТаблицаДокументов.Колонки.Документ;
	КонецЕсли;
	   
КонецПроцедуры

Процедура ОКНажатие(Элемент)
		
	Спис = Новый СписокЗначений; 
	мСохрФормат = ВосстановитьЗначение(ЭтаФорма.ВладелецФормы.мИмяДляХраненияНастроек);
		
	Для каждого Строка из ТаблицаДокументов Цикл
		Если Строка.Выгружать = Истина Тогда
			
			Если мСохрФормат = Неопределено Или мСохрФормат[Строка.ТипДокумента] = Неопределено Тогда	
				сТекст = "Не задана настройка для типа документа: ";
				сТекст = сТекст + Строка.ТипДокумента;
				Предупреждение(сТекст, 0, " Ошибка");
				Возврат;
			КонецЕсли;
			
			Спис.Добавить(Строка.ТипДокумента);	
		КонецЕсли;
	КонецЦикла;
	Спис.СортироватьПоЗначению();
	НажатаКнопкаOK = Истина;
	СохранитьЗначение(ЭтаФорма.ВладелецФормы.мИмяДляХраненияНастроек+"_ФормаСпискаДокументов", Спис);
	
	Закрыть();
	
КонецПроцедуры

Процедура ТаблицаДокументовНастройкаЗаполненияНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуНастройки();
	
КонецПроцедуры

Функция ПолучитьНастройкуЗаполненияДокумента(ТипДокумента) Экспорт
	
	НастройкаЗаполнения = "";
	ФормаНастройкиЗаполненияДокумента = ПолучитьФорму("ФормаФорматЗагрузки", ЭтаФорма);
	ФормаНастройкиЗаполненияДокумента.ТипДокумента = ТипДокумента;
	СохраненнаяНастройка = ФормаНастройкиЗаполненияДокумента.ПолучитьСохраненнуюНастройку();
	Если СохраненнаяНастройка <> Неопределено тогда
		Если СохраненнаяНастройка.Свойство("НастройкаПоДокументам") и СохраненнаяНастройка.НастройкаПоДокументам.Свойство(ТипДокумента)
			и не СохраненнаяНастройка.НастройкаПоДокументам[ТипДокумента].ИспользоватьСтандартнуюНастройку Тогда
			 НастройкаЗаполнения = "настройка пользователя";
		КонецЕсли;
	КонецЕсли;
	
	Возврат НастройкаЗаполнения;
	 
КонецФункции

Процедура ОткрытьФормуНастройки()
	
	ТекущаяСтрока = ЭлементыФормы.ТаблицаДокументов.ТекущаяСтрока;
	ФормаНастройкиЗаполненияДокумента = ПолучитьФорму("ФорматВыгрузкиФорма", ЭтаФорма);
	ФормаНастройкиЗаполненияДокумента.ТипДокумента = ТекущаяСтрока.ТипДокумента;
	ФормаНастройкиЗаполненияДокумента.ТипДокументаСиноним = ТекущаяСтрока.Документ;
	ФормаНастройкиЗаполненияДокумента.ОткрытьМодально();	
	
	ТекущаяСтрока.НастройкаЗаполнения = ФормаНастройкиЗаполненияДокумента.ТипДокументаСмартс;
	
КонецПроцедуры

Процедура КоманднаяПанельФормыВерхняяСправка(Кнопка)
	
	ФормаСправки = ПолучитьФорму("ФормаСправки");
	ФормаСправки.МакетСправки = "Выгрузка данных на ТСД Настройки заполнения.mht";
	ФормаСправки.Заголовок = "Справка к форме """+ЭтаФорма.Заголовок+""" обработки """+ ОбработкаОбъект.Метаданные().Синоним+"""";
	
	Если НЕ ФормаСправки.Открыта() Тогда
		ФормаСправки.Открыть();
	Иначе
		ФормаСправки.ОбновитьДанные();
	КонецЕсли; 

КонецПроцедуры

Процедура ТаблицаДокументовПередУдалением(Элемент, Отказ)
	Отказ = истина;
КонецПроцедуры

Процедура ТаблицаДокументовПередНачаломДобавления(Элемент, Отказ, Копирование)
	Отказ = истина;
КонецПроцедуры

НажатаКнопкаOK = ложь; 