Перем ОтчетПоВерсионированию Экспорт;

Функция РазницаТаблицЗначений(Таблица0, Таблица1, Измерения) Экспорт
    
    ВсеКолонки = "";
    Для Каждого Колонка Из Таблица0.Колонки Цикл 
        ВсеКолонки = ВсеКолонки + ", " + Колонка.Имя
    КонецЦикла;
    ВсеКолонки = Сред(ВсеКолонки, 2);
    
    Таблица = Таблица1.Скопировать();    
    
    Таблица.Колонки.Добавить("Знак", Новый ОписаниеТипов("Число"));
    
    Таблица.ЗаполнитьЗначения(1, "Знак");
    
    Для Каждого Строка Из Таблица0 Цикл ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Строка) КонецЦикла;
    
    Таблица.Колонки.Добавить("Счёт");
    Таблица.ЗаполнитьЗначения(1, "Счёт");
    
    Таблица.Свернуть(ВсеКолонки, "Знак, Счёт");
    
    Ответ = Таблица.Скопировать(Новый Структура("Счёт", 1), ВсеКолонки + ", Знак");
    
    Ответ.Сортировать(Измерения);
    
    Возврат Ответ
    
КонецФункции

Функция ПолучитьТаблицуВерсийПоЗапросу(Версии,СообщениеСостояния,ИндикаторПроцесса,Форма,ДолжноБыть=0) Экспорт
#Область Инициализация_таблицы
	ТаблицаОригинальныхВерсий=Новый ТаблицаЗначений;
	
	ТаблицаОригинальныхВерсий.Колонки.Добавить("Документ",		ЭтотОбъект.Метаданные().ТабличныеЧасти.СписокДокументовДляАнализа.Реквизиты.Документ.Тип);
	ТаблицаОригинальныхВерсий.Колонки.Добавить("НомерВерсии",	ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(10,0));
	ТаблицаОригинальныхВерсий.Колонки.Добавить("ДатаВерсии",	ОбщегоНазначения.ПолучитьОписаниеТиповДаты(ЧастиДаты.ДатаВремя));
	ТаблицаОригинальныхВерсий.Колонки.Добавить("АвторВерсии",	Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	
	ТаблицаОригинальныхВерсий.Колонки.Добавить("НомерСтроки",	ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(10,0));
	ТаблицаОригинальныхВерсий.Колонки.Добавить("Номенклатура",	Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаОригинальныхВерсий.Колонки.Добавить("Количество",	ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(15,3));	
#КонецОбласти
	Состояние(СообщениеСостояния);
	Форма.ЭлементыФормы[ИндикаторПроцесса].МаксимальноеЗначение=Версии.Количество();
	ИндикаторДокументов=0;
	Пока Версии.Следующий() Цикл
		//Получим объект
		ОтчетПоВерсионированию.СсылкаНаОбъект=Версии.Документ;
		Оригинал=ОтчетПоВерсионированию.ПолучитьОбъектИзXML(ОтчетПоВерсионированию.ПолучитьСтрокуXMLПоНомеруВерсии(Версии.НомерВерсии));
		Форма[ИндикаторПроцесса]=Форма[ИндикаторПроцесса]+1;
		Сч=0;
		Пока Сч < Оригинал.Товары.Количество() Цикл
			СтрокаВерсий=ТаблицаОригинальныхВерсий.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаВерсий,Версии);
			ЗаполнитьЗначенияСвойств(СтрокаВерсий,Оригинал.Товары[сч],"НомерСтроки,Номенклатура,Количество");
			Сч=Сч+1;
		КонецЦикла;
		Пока Сч < ДолжноБыть Цикл
			СтрокаВерсий=ТаблицаОригинальныхВерсий.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаВерсий,Версии);
			СтрокаВерсий.НомерСтроки=Сч;
			Сч=Сч+1;
		КонецЦикла;
	КонецЦикла;
	Состояние("");
	Возврат ТаблицаОригинальныхВерсий;
КонецФункции

Функция ИнициализацияИтоговойТаблицы() Экспорт
	Таблица=Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Документ",			ЭтотОбъект.Метаданные().ТабличныеЧасти.СписокДокументовДляАнализа.Реквизиты.Документ.Тип);
	Таблица.Колонки.Добавить("НомерВерсииОригинал",	ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(10,0));
	Таблица.Колонки.Добавить("ДатаВерсииОригинал",	ОбщегоНазначения.ПолучитьОписаниеТиповДаты(ЧастиДаты.ДатаВремя));
	Таблица.Колонки.Добавить("АвторВерсииОригинал",	Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	Таблица.Колонки.Добавить("НомерВерсии",			ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(10,0));
	Таблица.Колонки.Добавить("ДатаВерсии",			ОбщегоНазначения.ПолучитьОписаниеТиповДаты(ЧастиДаты.ДатаВремя));
	Таблица.Колонки.Добавить("АвторВерсии",			Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	Таблица.Колонки.Добавить("ЧислоОтличий",		ОбщегоНазначения.ПолучитьОписаниеТиповЧисла(10,0));
	
	Возврат Таблица;
КонецФункции
