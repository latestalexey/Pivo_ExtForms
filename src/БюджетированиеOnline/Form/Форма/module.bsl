
Процедура КнопкаВыполнитьНажатие(Кнопка)
Перем МассивДатДокументов;
	МассивДатДокументов=Новый Массив;
	Выполнить(Реквизит1.ФормированияПериодовДокументов);
	
	ФормированиеДанных=Обработки.IT_ОбменСПодсистемойБюджетирование.Создать();
	ФормированиеДанных.ВидБюджета=Реквизит1.НастройкаИмпорта.ВидБюджета;
	ФормированиеДанных.ЗапросБюджета=Реквизит1.НастройкаИмпорта;
	ФормированиеДанных.СписокАналитическихРазрезов.Очистить();
	
	Для Каждого ДатаФормирования из МассивДатДокументов Цикл
		ФормированиеДанных.ДатаНачала=ДатаФормирования;
		ФормированиеДанных.ДатаОкончания=КонецМесяца(ДатаФормирования);
		
		Запрос=ФормированиеДанных.ПолучитьЗапросПоНастройке();
		ФормированиеДанных.ОбработкаРезультатаЗапроса(Запрос.ВыполнитьПакет());
		
		//фильтация по статьям затрат
		ТабДанных=ФормированиеДанных.СписокАналитическихРазрезов.Выгрузить();
		МассивСтатейДляУдаления=ИТИИндустрияОбщийМодульКлиентСервер.ЗаполнитьМассивУникальнымиЗначениями(ТабДанных.ВыгрузитьКолонку("СтатьяБюджета"));
		Для Каждого Эл Из Реквизит1.СоставСтатей Цикл
			Индекс=МассивСтатейДляУдаления.Найти(Эл.СтатьяБюджетирования);
			Если Индекс<>Неопределено Тогда
				МассивСтатейДляУдаления.Удалить(Индекс);
			КонецЕсли;
		КонецЦикла;;
		Для Каждого Эл Из МассивСтатейДляУдаления Цикл
			СтрокиДляУдаления=ТабДанных.НайтиСтроки(Новый Структура("СтатьяБюджета",Эл));
			Для Каждого СтрокаУдаления Из СтрокиДляУдаления Цикл
				ТабДанных.Удалить(СтрокаУдаления);
			КонецЦикла;
		КонецЦикла;
		
		//Создание/обновление документов
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	IT_ОтражениеФактическихДанныхБюджетирования.Ссылка,
		|	IT_ОтражениеФактическихДанныхБюджетирования.Подразделение
		|ИЗ
		|	Документ.IT_ОтражениеФактическихДанныхБюджетирования КАК IT_ОтражениеФактическихДанныхБюджетирования
		|ГДЕ
		|	IT_ОтражениеФактическихДанныхБюджетирования.НастройкаАвтоформирования = &НастройкаАвтоформирования
		|	И IT_ОтражениеФактическихДанныхБюджетирования.Период = &Период
		|	И НЕ IT_ОтражениеФактическихДанныхБюджетирования.ПометкаУдаления";
		
		Запрос.УстановитьПараметр("НастройкаАвтоформирования", Реквизит1);
		Запрос.УстановитьПараметр("Период", ДатаФормирования);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		СоответствиеДокументов=Новый Соответствие;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			СоответствиеДокументов.Вставить(ВыборкаДетальныеЗаписи.Подразделение,ВыборкаДетальныеЗаписи.Ссылка);
		КонецЦикла;
	
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ТаблицаДанных.СтатьяБюджета,
		             |	ТаблицаДанных.Проект,
		             |	ТаблицаДанных.Контрагент,
		             |	ТаблицаДанных.Номенклатура,
		             |	ТаблицаДанных.ЕдиницаИзмерения,
		             |	ТаблицаДанных.Количество,
		             |	ТаблицаДанных.Сумма,
		             |	ТаблицаДанных.Подразделение,
		             |	ТаблицаДанных.ВидБюджета,
		             |	ТаблицаДанных.Период
		             |ПОМЕСТИТЬ ВТ_ДанныеБюджетирования
		             |ИЗ
		             |	&ТаблицаДанных КАК ТаблицаДанных
		             |;
		             |
		             |////////////////////////////////////////////////////////////////////////////////
		             |ВЫБРАТЬ
		             |	ВТ_ДанныеБюджетирования.СтатьяБюджета,
		             |	ВТ_ДанныеБюджетирования.Проект,
		             |	ВТ_ДанныеБюджетирования.Контрагент,
		             |	ВТ_ДанныеБюджетирования.Номенклатура,
		             |	ВТ_ДанныеБюджетирования.ЕдиницаИзмерения,
		             |	ВТ_ДанныеБюджетирования.Количество,
		             |	ВТ_ДанныеБюджетирования.Сумма,
		             |	ВТ_ДанныеБюджетирования.Подразделение КАК Подразделение,
		             |	ВТ_ДанныеБюджетирования.ВидБюджета,
		             |	ВТ_ДанныеБюджетирования.Период
		             |ИЗ
		             |	ВТ_ДанныеБюджетирования КАК ВТ_ДанныеБюджетирования
		             |ИТОГИ ПО
		             |	Подразделение";
		Запрос.УстановитьПараметр("ТаблицаДанных",ТабДанных);
		ВыборкаПодразделений=Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПодразделений.Следующий() Цикл
			Док=СоответствиеДокументов.Получить(ВыборкаПодразделений.Подразделение);
			Если Док=Неопределено Тогда
				Док=Документы.IT_ОтражениеФактическихДанныхБюджетирования.СоздатьДокумент();
				Док.ВидБюджета=Реквизит1.НастройкаИмпорта.ВидБюджета;
				Док.Дата=ТекущаяДата();
				Док.ДатаНачала=ДатаФормирования;
				Док.ДатаОкончания=КонецМесяца(ДатаФормирования);
				Док.НастройкаАвтоформирования=Реквизит1;
				Док.Ответственный=глЗначениеПеременной("глТекущийПользователь");
				Док.Период=ДатаФормирования;
				Док.Подразделение=ВыборкаПодразделений.Подразделение;
			Иначе
				Док=Док.ПолучитьОбъект();
			КонецЕсли;
			Док.СписокАналитическихРазрезов.Очистить();
			ВыборкаДанные=ВыборкаПодразделений.Выбрать();
			Пока ВыборкаДанные.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(Док.СписокАналитическихРазрезов.Добавить(),ВыборкаДанные);
			КонецЦикла;
			Док.Записать(РежимЗаписиДокумента.Проведение);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры


