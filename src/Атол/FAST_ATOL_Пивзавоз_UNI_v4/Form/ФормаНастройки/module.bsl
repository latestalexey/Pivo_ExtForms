///////////////////////////////////////////////////////////////////////////////
//// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мНетОшибки;
Перем мМодель Экспорт;
Перем мПараметры Экспорт;

///////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события "Перед открытием" формы.
//
// Параметры:
//  Отказ                - <Булево>
//                       - Признак отказа от открытия формы. Если в теле
//                         процедуры-обработчика установить данному параметру
//                         значение Истина, открытие формы выполнено не будет.
//                         Значение по умолчанию: Ложь 
//
//  СтандартнаяОбработка - <Булево>
//                       - В данный параметр передается признак выполнения
//                         стандартной (системной) обработки события. Если в
//                         теле процедуры-обработчика установить данному
//                         параметру значение Ложь, стандартная обработка
//                         события производиться не будет. Отказ от стандартной
//                         обработки не отменяет открытие формы.
//                         Значение по умолчанию: Истина 
//
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)

	ЭлементыФормы.Драйвер.ЦветТекстаПоля = ЦветаСтиля.ЦветТекстаФормы;
	Драйвер = "Не требуется";
	ЭлементыФормы.Версия.ЦветТекстаПоля = ЦветаСтиля.ЦветТекстаФормы;
	Версия  = "Не определена";

	ЭлементыФормы.НастройкаККМOffline.Значение = "Настройка параметров ККМ Offline """ + мМодель + """";

	Если мПараметры.Количество() = 0 Тогда
		БазаТоваров  = "";
		ФайлОтчета   = "";
		ФлагВыгрузки = "";
	Иначе
		БазаТоваров         = мПараметры.БазаТоваров;
		ФайлОтчета          = мПараметры.ФайлОтчета;
		ФлагВыгрузки        = мПараметры.ФлагВыгрузки;
	КонецЕсли;

КонецПроцедуры // ПередОткрытием()

// Процедура представляет обработчик события "Нажатие" кнопки
// "ОК" командной панели "ОсновныеДействияФормы".
//
// Параметры:
//  Кнопка - <КнопкаКоманднойПанели>
//         - Кнопка, с которой связано данное событие (кнопка "ОК").
//
Процедура ОсновныеДействияФормыОК(Кнопка)

	ОписаниеОшибки = "";

	Если Не ЗначениеЗаполнено(БазаТоваров) Тогда
		ОписаниеОшибки = " - Не указано имя файла базы товаров.";
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ФайлОтчета) Тогда
		ОписаниеОшибки = ОписаниеОшибки + ?(ПустаяСтрока(ОписаниеОшибки),
		                                    "",
		                                    "
		                                    |")
		                                + " - Не указано имя файла отчета.";
	КонецЕсли;

	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		мПараметры = Новый Структура;
		мПараметры.Вставить("БазаТоваров",  БазаТоваров);
		мПараметры.Вставить("ФайлОтчета",   ФайлОтчета);
		мПараметры.Вставить("ФлагВыгрузки", ФлагВыгрузки);

		Ошибка    = Перечисления.ТООшибкиОбщие.ПустаяСсылка();

		Закрыть(КодВозвратаДиалога.ОК);
	Иначе
		Предупреждение("При проверке были обнаружены следующие ошибки:
		               |" + ОписаниеОшибки);
	КонецЕсли;

КонецПроцедуры // ОсновныеДействияФормыОК()

// Процедура представляет обработчик события "Нажатие" кнопки
// "Отмена" командной панели "ОсновныеДействияФормы".
//
// Параметры:
//  Кнопка - <КнопкаКоманднойПанели>
//         - Кнопка, с которой связано данное событие (кнопка "Отмена").
//
Процедура ОсновныеДействияФормыОтмена(Кнопка)

	Ошибка = Перечисления.ТООшибкиОбщие.ПустаяСсылка();

	Закрыть(КодВозвратаДиалога.Отмена);

КонецПроцедуры // ОсновныеДействияФормыОтмена()

// Процедура - обработчик события "Начало выбора" поля ввода
// "БазаТоваров".
//
// Параметры:
//  Элемент              - <ПолеВвода>
//                       - Поле ввода, с которым связано данное событие
//                         ("БазаТоваров").
//
//  СтандартнаяОбработка - <Булево>
//                       - В данный параметр передается признак выполнения
//                         стандартной (системной) обработки события. Если
//                         в теле процедуры-обработчика установить данному
//                         параметру значение Ложь, стандартная обработка
//                         события производиться не будет
//                         Значение по умолчанию: Истина.
//
Процедура БазаТоваровНачалоВыбора(Элемент, СтандартнаяОбработка)

	ВыбратьФайл(РежимДиалогаВыбораФайла.Открытие, БазаТоваров);

	СтандартнаяОбработка = Ложь;

КонецПроцедуры // БазаТоваровНачалоВыбора()

// Процедура - обработчик события "Начало выбора" поля ввода
// "ФлагВыгрузки".
//
// Параметры:
//  Элемент              - <ПолеВвода>
//                       - Поле ввода, с которым связано данное событие
//                         ("ФлагВыгрузки").
//
//  СтандартнаяОбработка - <Булево>
//                       - В данный параметр передается признак выполнения
//                         стандартной (системной) обработки события. Если
//                         в теле процедуры-обработчика установить данному
//                         параметру значение Ложь, стандартная обработка
//                         события производиться не будет
//                         Значение по умолчанию: Истина.
//
Процедура ФлагВыгрузкиНачалоВыбора(Элемент, СтандартнаяОбработка)

	ВыбратьФайл(РежимДиалогаВыбораФайла.Открытие, ФлагВыгрузки);

	СтандартнаяОбработка = Ложь;

КонецПроцедуры // ФлагВыгрузкиНачалоВыбора()

// Процедура - обработчик события "Начало выбора" поля ввода
// "ФайлОтчета".
//
// Параметры:
//  Элемент              - <ПолеВвода>
//                       - Поле ввода, с которым связано данное событие
//                         ("ФайлОтчета").
//
//  СтандартнаяОбработка - <Булево>
//                       - В данный параметр передается признак выполнения
//                         стандартной (системной) обработки события. Если
//                         в теле процедуры-обработчика установить данному
//                         параметру значение Ложь, стандартная обработка
//                         события производиться не будет
//                         Значение по умолчанию: Истина.
//
Процедура ФайлОтчетаНачалоВыбора(Элемент, СтандартнаяОбработка)

	ВыбратьФайл(РежимДиалогаВыбораФайла.Открытие, ФайлОтчета);

	СтандартнаяОбработка = Ложь;

КонецПроцедуры // ФайлОтчетаНачалоВыбора()

///////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура осуществляет открытие диалога выбора файлов.
//
// Параметры:
//  РежимОткрытияФайла - РежимДиалогаВыбораФайла.
//  ИмяФайла - Строка. Переменная, в которую будет помещен результат выбора.
//
Процедура ВыбратьФайл(РежимОткрытияФайла, ИмяФайла)

	Диалог = Новый ДиалогВыбораФайла(РежимОткрытияФайла);
	Диалог.МножественныйВыбор = Ложь;
	Диалог.ПолноеИмяФайла = ИмяФайла;

	Если Диалог.Выбрать() Тогда
		ИмяФайла = Диалог.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры // ВыбратьФайл()